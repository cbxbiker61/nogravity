#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(nogravity, 1.98, http://sourceforge.net/projects/nogravity)
AM_INIT_AUTOMAKE(nogravity, 1.98)

AC_CONFIG_SRCDIR(preferences.c)
AC_CONFIG_HEADER(autoconfig.h)
AC_CONFIG_FILES([Makefile])

# Define arguments.
AC_ARG_ENABLE(debug,[  --enable-debug          Enable debug mode],
  [if test "$enableval" = "yes"
   then
     AC_DEFINE([_DEBUG], 1, [Define to 1 if debug mode is enabled.])
   fi])
AC_ARG_ENABLE(sound, [  --enable-sound=type     Specify sound library (sdl_mixer or openal)],
  [sound="$enableval"])

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM

# Checks for standard headers and functions.
AC_HEADER_STDC
AC_CHECK_HEADERS([stddef.h stdint.h stdlib.h string.h unistd.h])
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([atexit getcwd memmove memset mkdir pow strchr strstr strtol])

# Checks for pthreads.
AC_CHECK_HEADERS([pthread.h], , AC_MSG_ERROR([Can't find pthread headers.]))
AC_CHECK_LIB([pthread], [pthread_create], , AC_MSG_ERROR([Can't find pthread library.]))

# Checks for SDL.
AM_PATH_SDL(1.2.0, , AC_MSG_ERROR([Can't find SDL 1.2 or later.]))

# Checks for OpenGL.
AC_CHECK_HEADERS([GL/gl.h], , AC_MSG_ERROR([Can't find OpenGL headers.]))
AC_CHECK_LIB([GL], [glBegin], , AC_MSG_ERROR([Can't find OpenGL library.]))

# Checks for OpenAL (optional).
if test "$sound" = "openal"
then
  AC_CHECK_HEADERS([AL/al.h AL/alc.h], , AC_MSG_ERROR([Can't find OpenAL headers for sound.]))
  AC_CHECK_LIB([openal], [alGenSources], , AC_MSG_ERROR([Can't find OpenAL library for sound.]))
fi

# Checks for SDL_mixer (optional).
if test "$sound" = "sdl_mixer"
then
  AC_CHECK_HEADERS([SDL/SDL_mixer.h], , AC_MSG_ERROR([Can't find SDL_mixer headers for sound.]))
  AC_CHECK_LIB([SDL_mixer], [Mix_OpenAudio], , AC_MSG_ERROR([Can't find SDL_mixer library for sound.]))
fi

# Checks for libpng.
AC_CHECK_HEADERS([png.h], , AC_MSG_ERROR([Can't find libpng header.]))
AC_CHECK_LIB([png], [png_decompress_chunk], , AC_MSG_ERROR([Can't find libpng library.]))

# Checks for Ogg.
AC_CHECK_LIB([ogg], [ogg_stream_init], , AC_MSG_ERROR([Can't find Ogg library.]))

# Checks for Vorbis.
AC_CHECK_HEADERS([vorbis/codec.h], , AC_MSG_ERROR([Can't find Vorbis header.]))
AC_CHECK_LIB([vorbis], [vorbis_synthesis_init], , AC_MSG_ERROR([Can't find Vorbis library.]))

# Output it all.
AC_OUTPUT
